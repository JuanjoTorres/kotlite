# http://mdaines.github.io/grammophone/

# En la producciÃ³n JOIN hay que sustituir el OR por otro sÃ­mbolo:
# Sustituir [ JOIN -> || ] por [ JOIN -> $$ ]

PROGRAM -> DECLS FUNCTIONS .
FUNCTIONS -> FUNCTIONS FUNCTION .
FUNCTIONS -> .
FUNCTION  -> fun id ( ARGS ) : BASIC { DECLS STATMENTS RTNPART } .
RTNPART -> return FACTOR .
RTNPART -> .
DECLS -> DECLS DECL .
DECLS -> .
DECL -> TYPE id : BASIC ; .
ARGS -> ARGS ARG .
ARGS -> .
ARG -> id : BASIC; .
STATMENTS -> STATMENTS STATMENT .
STATMENTS -> .
STATMENT -> id = BOOL ; .
STATMENT -> if ( BOOL ) { DECLS STATMENTS } ELSEPART .
STATMENT -> while ( BOOL ) { DECLS STATMENTS } .
ELSEPART -> else { DECLS STATMENTS } .
ELSEPART -> .
BOOL -> BOOL JOIN RELATION .
BOOL -> RELATION .
RELATION -> EXPR OPREL EXPR .
RELATION -> EXPR .
EXPR -> EXPR ADD TERM .
EXPR -> TERM .
TERM -> TERM MULT UNARY .
TERM -> UNARY .
UNARY -> ! UNARY .
UNARY -> FACTOR .
ADD	-> + .
ADD -> - .
MULT -> * .
MULT -> / .
JOIN -> || .
JOIN -> && .
OPREL -> == .
OPREL -> != .
OPREL -> < .
OPREL -> <= .
OPREL -> > .
OPREL -> >= .
TYPE -> var .
TYPE -> val .
BASIC -> Int .
BASIC -> Float .
BASIC -> Char .
BASIC -> String .
BASIC -> Boolean .
BASIC -> None .
FACTOR -> ( BOOL ) .
FACTOR -> id .
FACTOR -> literal .
FACTOR -> num .
FACTOR -> true .
FACTOR -> false .